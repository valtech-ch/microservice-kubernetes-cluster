plugins {
    id 'io.spring.dependency-management' version '1.1.4'
    id "com.google.protobuf" version "0.9.4"
    id 'java'
    id 'java-library'
    id 'eclipse'
    id 'idea'
}

project.description = 'Persistence API'

ext {
    lombokVersion = '1.18.30'
}

apply from: "${rootProject.projectDir}/gradle/codeStyle.gradle"

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:4.26.0'
    }
    // Locate the codegen plugins
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.60.1'
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:1.2.4"
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin whose spec is defined above, without
            // options.  Note the braces cannot be omitted, otherwise the
            // plugin will not be added. This is because of the implicit way
            // NamedDomainObjectContainer binds the methods.
            grpc { }
            reactor { }
        }
    }
}

jar {
    enabled = true
}

sonarqube {
    properties {
        property "sonar.tests", ""
    }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'io.grpc:grpc-protobuf:1.59.0'
    implementation 'io.grpc:grpc-stub:1.59.0'
    implementation 'io.projectreactor:reactor-core:3.6.4'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.4'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}
