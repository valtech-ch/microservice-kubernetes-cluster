plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.google.protobuf" version "0.8.18"
    id 'java'
    id 'java-library'
    id 'eclipse'
    id 'idea'
}

project.description = 'Persistence API'

ext {
    lombokVersion = '1.18.22'
}

apply from: "${rootProject.projectDir}/gradle/codeStyle.gradle"

sourceCompatibility = java_source_compatibility ?: 15
targetCompatibility = java_target_compatibility ?: 15

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.19.1'
    }
    // Locate the codegen plugins
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.43.0'
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:1.2.3"
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin whose spec is defined above, without
            // options.  Note the braces cannot be omitted, otherwise the
            // plugin will not be added. This is because of the implicit way
            // NamedDomainObjectContainer binds the methods.
            grpc { }
            reactor { }
        }
    }
}

jar {
    enabled = true
}

sonarqube {
    properties {
        property "sonar.tests", ""
    }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'io.grpc:grpc-protobuf:1.43.0'
    implementation 'io.grpc:grpc-stub:1.43.1'
    implementation 'io.projectreactor:reactor-core:3.4.13'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}
