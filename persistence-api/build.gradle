plugins {
    id 'io.spring.dependency-management' version '1.1.0'
    id "com.google.protobuf" version "0.9.2"
    id 'java'
    id 'java-library'
    id 'eclipse'
    id 'idea'
}

project.description = 'Persistence API'

ext {
    lombokVersion = '1.18.24'
}

apply from: "${rootProject.projectDir}/gradle/codeStyle.gradle"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.21.12'
    }
    // Locate the codegen plugins
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.52.0'
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:1.2.3"
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin whose spec is defined above, without
            // options.  Note the braces cannot be omitted, otherwise the
            // plugin will not be added. This is because of the implicit way
            // NamedDomainObjectContainer binds the methods.
            grpc { }
            reactor { }
        }
    }
}

jar {
    enabled = true
}

sonarqube {
    properties {
        property "sonar.tests", ""
    }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
    implementation 'io.grpc:grpc-protobuf:1.52.0'
    implementation 'io.grpc:grpc-stub:1.52.1'
    implementation 'io.projectreactor:reactor-core:3.5.2'
    implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.2.3'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}
